#!/usr/bin/env escript

usage() ->
  io:format("~s <Application> <Version> [<UpgradeFrom> ...]~n",[escript:script_name()]),
  halt().

main([]) ->
  usage();

main([ ApplicationName, CurrentVersion | OldVersions ]) ->
  ScriptDir = filename:dirname(escript:script_name()),
  BaseDir = filename:absname("..",ScriptDir),
  LibGlob = filename:absname("lib/*/ebin",BaseDir),
  EBin = filename:absname("ebin",BaseDir),
  Releases = filename:absname("releases",BaseDir),
  ReleaseName = ApplicationName ++ "-" ++ CurrentVersion,
  GetRel = fun(Ver) -> filename:join( [ Releases, ApplicationName ++ "-" ++ Ver ] ) end,
  [ Release | OldReleases ] = lists:map( GetRel, [CurrentVersion | OldVersions] ),

  io:format("Building Release: ~s~n",[ReleaseName]),

  io:format("- building boot script: "),
  case systools:make_script(Release,[local,{path,[Releases,EBin,LibGlob]},{outdir,"releases/"},silent]) of
    {ok,_,[]} ->
      io:format("ok~n");
    {ok,Mod0,Warnings0} -> 
      io:format("ok~n--BEGIN-WARNINGS--~n~s--END-WARNINGS--~n",[apply(Mod0,format_warning,[Warnings0])]);
    {error,Mod0,Error0} ->
      io:format("ERROR~n~s",[apply(Mod0,format_error,[Error0])]),
      halt()
  end,

  io:format("- building release update script: "),
  case systools:make_relup(Release,OldReleases,OldReleases,[{path,[Releases,EBin,LibGlob]},{outdir,"releases/"},silent]) of
    {ok,_,_,[]} ->
      io:format("ok~n");
    {ok,_RelUp,Mod1,Warnings1} -> 
      io:format("ok~n--BEGIN-WARNINGS--~n~s--END-WARNINGS--~n",[apply(Mod1,format_warning,[Warnings1])]);
    {error,Mod1,Error1} ->
      io:format("ERROR~n~s",[apply(Mod1,format_error,[Error1])]),
      halt()
  end,

  io:format("- building tar file: "),
  case systools:make_tar(Release,[{path,[Releases,EBin,LibGlob]},{outdir,"releases/"},silent]) of
    {ok,_,[]} ->
      io:format("ok~n");
    {ok,Mod2,Warnings2} -> 
      io:format("ok~n--BEGIN-WARNINGS--~n~s--END-WARNINGS--~n",[Mod2:format_warning(Warnings2)]);
    {error,Mod2,Error2} ->
      io:format("ERROR~n~s",[apply(Mod2,format_error,[Error2])]),
      halt()
  end.

